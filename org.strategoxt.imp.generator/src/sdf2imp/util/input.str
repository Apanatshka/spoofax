module sdf2imp/util/input

imports
  libstratego-gpp
  libstratego-sglr
  
  EditorService
  
  sdf2imp/lib/-
  sdf2imp/services/common
  sdf2imp/services/configure-main-descriptor

strategies

  /**
   * Given a location and filename (no extension),
   * parse an editor descriptor, including all imports.
   */
  input-descriptor-file =
    {| IsImported:
      ?import;
      name := <conc-strings> (<main-descriptor-name>, ".packed");
      <fetch-imports> Module(name, Imports([Import(import)]), [])
    |}
  
  set-input-dir-prefix =
    with(string-ends-with(|"/"));
    rules(InputDirPrefix := <id>)
  
  add-input-dir-prefix =
    <set-input-dir-prefix> <conc-strings> (<InputDirPrefix <+ !"">, <id>)

/** @internal */
strategies
  
  include-input-dir-prefix =
    try(<conc-strings> (<InputDirPrefix>, <id>))
    
  // TODO: Optimize - file input/parsing caching
  
  // TODO: Multi-directory support (-I)
  
  input-descriptor-import =
    ?Import(name);
    !FILE(<add-extension; include-input-dir-prefix; file-exists; debug(!"found file ") <+ add-extension; file-exists; debug(!"found file ")> (name, "esv"));
    parse-xtc-file-report-errors(|<parse-table-descriptor>, "Module");
    if oncetd(?amb(_)) then
      fatal-err(|"Parsed module has ambiguities")
    end;
    
    (
      ?Module(name, _, _)
    <+
      try(<OnImportFailure> Import(name)); fail // trigger failure event
    <+
      if <has-extension(|"esv")> <InputFile> then
        fatal-err(|"Module name does not match file name")
      else
        err(|"Module name does not match file name")
      end
    )
  <+
    ?ImportRenamed(name, tail);
    <input-descriptor-import> Import(name);
    fetch-imports;
    alltd(sort-add-suffix(|tail))
    
  // TODO: Sort renaming could be inferred from the .def file
  
  sort-add-suffix(|tail):
    Sort(name) -> Sort(<conc-strings> (name, "_", tail))
  
  sort-add-suffix(|tail):
    ListSort(name) -> ListSort(<conc-strings> (name, "_", tail))
  
  fetch-imports:
    Module(name, Imports(imports), definitions) ->
    Module(name, Imports(imports'), definitions')
    where
      name':= <strip-annos> name;
      rules(IsImported: name');
      
      !imports;
      map(
        risky(
          where(?Import(<strip-annos>); IsImported); ![] //if it is already imported do nothing
          
        <+
          
          input-descriptor-import;
          fetch-imports
        <+
          OnImportFailure
        | "Unable to import module"
        )
      ) => imported;
      
      flatten-list; 
      if <?[]> imported then //check if there is only already imported modules
	      imports' := imports;
	      definitions' := definitions      
	  else
	      imported-defs    := <mapconcat(?Module(_, _, <id>))>;
	      definitions'     := <conc> (definitions, <id>);
	      imported-imports := <filter(?Module(_, Imports(<id>), _))> imported;
	      imports'         := <concat> [imports | <id>]
	  end
	  
  fetch-imports:
    Module(name, NoImports(), _) -> <id>
    where
    	name' := <strip-annos> name;
    	rules(IsImported: name')

  is-older-than =
    ?(file, otherfiles);
    basetime := <file-exists; modification-time> file;
    !otherfiles;
    list-loop(
      <lt> (basetime, <file-exists < modification-time + verbose-msg(!"Does not exist:", id); fail>)
    )
    

/**
 * @internal
 */
strategies
  
  parse-table-descriptor =
    CachedParseTableDescriptor
  <+
    table := <open-parse-table> <import-term(EditorService.tbl)>;
    rules(CachedParseTableDescriptor := table)

