module sdf2imp/services/create-analysis-descriptor

imports
  libstratego-lib
  
  sdf2imp/util/-
  sdf2imp/project/create-trans

strategies

  create-analysis-descriptor =
    output-initial-descriptor-file(
      <descriptor-name> "-Analysis"
    , provider := <conc-strings> ("include/", <trans-module-name>, ".ctree");
      !|[
        module <descriptor-name> "-Analysis"
        
        imports <descriptor-name> "-Analysis.generated"
        
        analysis
          ~
          ~// This file can be used for custom analysis rules.
          ~// See the imported file for a brief introduction and examples.
          ~
          provider: ~provider
          observer: editor-analyze
          ~
          ~// observer: analyze
      ]|
    );
    
    output-derived-descriptor-file(
      <descriptor-name> "-Analysis.generated"
    ,
      !|[
        module <descriptor-name> "-Analysis.generated"
        
        analysis
          ~
          ~// Analysis can be provided by an external Stratego program:
          ~//
          ~//   provider : stratego-program.ctree // (built using strc -F)
          ~//
          ~// Analysis is carried out by a rule of the form (ast, path, fullpath) -> (errors, warnings, infos)
          ~//
          ~//   observer : analysis-rule
      ]|
    )
