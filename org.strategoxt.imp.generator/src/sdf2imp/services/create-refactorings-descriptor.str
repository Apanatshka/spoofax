module sdf2imp/services/create-refactorings-descriptor

imports
  libstratego-lib
 
  sdf2imp/util/-
  sdf2imp/services/common
  sdf2imp/project/create-example-trans

strategies

  create-refactorings-descriptor =
    output-initial-descriptor-file(
      <descriptor-name> "-Refactorings"
    , refactorings-descriptor
    );
    
    output-derived-descriptor-file(
      <descriptor-name> "-Refactorings.generated"
    ,
      !|[
        module <descriptor-name> "-Refactorings.generated"

        keybindings Predefined Keybindings

          Shift + Alt + R  = "org.eclipse.jdt.ui.edit.text.java.rename.element" // Rename
          Shift + Alt + I  = "org.eclipse.jdt.ui.edit.text.java.inline" // Inline
          Shift + Alt + M  = "org.eclipse.jdt.ui.edit.text.java.extract.method" // Extract
          Shift + Alt + V  = "org.eclipse.jdt.ui.move.method" // Move
          Shift + Alt + C  = "org.eclipse.jdt.ui.change.method.signature" // Change Signature
          Shift + Alt + L  = "org.eclipse.jdt.ui.edit.text.java.extract.local.variable" // Extract Local Variable
          Shift + Ctrl + F = "org.eclipse.jdt.ui.edit.text.java.format" // Format
          Ctrl + I         = "org.eclipse.jdt.ui.edit.text.java.indent" // Correct Indentation
          Shift + Ctrl + C = "org.eclipse.jdt.ui.edit.text.java.toggle.comment" // Toggle Comment
          Shift + Ctrl + M = "org.eclipse.jdt.ui.edit.text.java.add.import" // Add Import
          Shift + Ctrl + O = "org.eclipse.jdt.ui.edit.text.java.organize.imports" // Organize Imports
      ]|  
    )
    
  refactorings-descriptor =
  	reconstruction-strategy := "construct-textual-change";
  	(GenerateMinimal
  	<
  	!|[
        module <descriptor-name> "-Refactorings"

        imports <descriptor-name> "-Refactorings.generated"

        refactorings
          ~~
          text reconstruction : ~reconstruction-strategy
          ~~
      ]|
  	+
  	!|[
        module <descriptor-name> "-Refactorings"

        imports <descriptor-name> "-Refactorings.generated"

        refactorings
          ~~
          text reconstruction : ~reconstruction-strategy
          ~~
          refactoring ID: "Rename Entity" = rename-refactoring (cursor)
          shortcut : Shift + Alt + R
          input
            identifier: "new name" = ""
      ]|
      )
 